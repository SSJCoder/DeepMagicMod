@onmain {
	&sfxLightSpell = caster_load( 'data/sounds/sfxLightSpell.ogg' );
	caster_loop( caster_load( 'data/sounds/atmCave.ogg' ), 1.0, 1.0 );
}

&CharLightSpell {
	// cast light spell
	Cdw += 0.35;
	r~, a~ = 6, arm.image_angle;
	&newLightSpell( arm.x+lenx(r,a), arm.y+leny(r,a), a, 3.5 );
	&newFXSpellThrust( arm.x+lenx(r,a), arm.y+leny(r,a), c_white, 0.15, a, 0.15 );
	
}

&newLightSpell (x, y, rot_, dmg_) {
	@with (create( x, y, &oLightSpell )) {
		// set depth
		depth = -125;
		
		// set properties
		rot = rot_;
		dmg = dmg_;
		
		// set speed
		spd = 500 * flc( 0.2 );
		
		// set sprite & mask
		sprite_index = sLightSpell;
		image_angle  = rot_;
		
		// stop animation
		image_speed = 0;

		// default size
		size = flu( 0.2 );
		
		UpdateScaling = @fn {
			a~= Time()*2*pi*3.5;
			image_xscale = size*(1+cos(a)*0.20);
			image_yscale = size*(1+sin(a)*0.20);
		}
		
		UpdateScaling();
		
		// sfx
		caster_play( &sfxLightSpell, 0.20*flc( 0.20 ), flc( 0.20 ) );
		
		// no destroy fx
		NoFXFlag = 0;
		
		@ret id;
		
	}
}

&oLightSpell => object_add();
@onmain {
	event( &oLightSpell, 'step', @fn {
		// move
		x+=lenx(spd*ts,rot);
		y+=leny(spd*ts,rot);
		
		// update scaling
		UpdateScaling();
		
		@test ( place_meeting( x, y, oBlock ) ) {
			// fx
			@test ( !NoFXFlag ) {
				&newFXBeam( x, y, c_aqua, 0.3, 0.2 );
			}
			// destroy
			destroy();
		}
		
		@test ( is_outside() ) {
			destroy();
		}
		
		enm~= instance_place( x, y, oEnemy );
		@test ( enm != noone ) {
			// deal damage to enemy
			&DealDamage( enm, dmg, 0.0 );
			
			// fx
			&newFXBeam( x, y, c_aqua, 0.3, 0.2 );
			
			// clear spell
			destroy();
		}
		
		@test ( place_meeting( x, y, oLavaTop ) ) {
			NoFXFlag = 1;
			   depth = 1500;
		}
		
		wasp~= instance_place( x, y, oWasp );
		@test ( wasp != noone ) {
			destroy( wasp );
		}
		
	});
	
	event( &oLightSpell, 'draw', @fn {
		draw_set_blend_mode( bm_add );
		draw_self();
		draw_set_blend_mode( bm_normal );
		
	});
}

